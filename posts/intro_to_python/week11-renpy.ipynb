{
  "cells": [
    {
      "cell_type": "raw",
      "metadata": {},
      "source": [
        "---\n",
        "title: 11 Ren'py\n",
        "# date: \"1970-01-01\"\n",
        "# categories: ['Python', 'lang:Chinese']\n",
        "description: \"The Ren'py script file shows the instructions in Ren'py\"\n",
        "execute: \n",
        "  message: false\n",
        "  warning: false\n",
        "editor_options: \n",
        "  chunk_output_type: console\n",
        "---"
      ],
      "id": "fabce9fc"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "### script.rpy in Ren'py\n",
        "Notice that at the time this course was teaching, the lastest version of Ren'py is 7.4, which is the last version using Python 2.7. Then, they shifted to Ren'py 8.0, which using Python 3."
      ],
      "id": "62a518ee"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "#| code-fold: true\n",
        "#| eval: false\n",
        "# 游戏的脚本可置于此文件中。\n",
        "init python:\n",
        "    # init python 语句在初始化阶段运行，早于其他游戏资源加载。\n",
        "    pass\n",
        "    # import numpy as np\n",
        "\n",
        "# 声明此游戏使用的角色。颜色参数可使角色姓名着色。\n",
        "define author = Character(\"张晴森\", color='#7955d1')\n",
        "define abc = Character(\"abc123\")\n",
        "\n",
        "# 游戏在此开始。\n",
        "\n",
        "label start:\n",
        "\n",
        "    # 显示一个背景。此处默认显示占位图，但您也可以在图片目录添加一个文件\n",
        "    # （命名为“bg room.png”或“bg room.jpg”）来显示。\n",
        "    \"在script中仅有字符串时为无角色对话\"\n",
        "    \n",
        "    abc \"上面定义了角色abc123，角色名加字符串为简单对话\"\n",
        "    author \"上面定义了角色{b}{i}{size=45}{alpha=0.7}{color=#7955d1}张晴森{/color}{/alpha}{/size}{/i}{/b}，角色名显示为所设置的颜色\"\n",
        "\n",
        "    \"文字标签\"\n",
        "    # https://www.renpy.cn/doc/text.html\n",
        "    \"用 \\{size\\} 标签设置文字大小\"\n",
        "    \"如{size=45}abc123{/size}和{size=15}abc123{/size}\"\n",
        "    \"用 \\{color\\} 标签设置文字颜色\"\n",
        "    \"如{color=#7955d1}abc123{/color}和{color=#86aa2e}abc123{/color}\"\n",
        "    \"用 \\{alpha\\} 标签设置文字透明度\"\n",
        "    \"如{alpha=0.5}abc123{/alpha}\"\n",
        "    \"用 \\{s\\} 标签设置文字粗体\"\n",
        "    \"如{s}删除线{/s}\"\n",
        "    \"用 \\{b\\} 标签设置文字粗体\"\n",
        "    \"如{b}bold{/b}\"\n",
        "    \"用 \\{i\\} 标签设置文字斜体\"\n",
        "    \"如{i}italic{/i}\"\n",
        "    \"结合使用\"\n",
        "    \"如{b}{size=45}{alpha=0.7}{color=#7955d1}abc123{/color}{/alpha}{/size}{/b}和{i}{size=15}{alpha=0.3}{color=#86aa2e}abc123{/alpha}{/size}{/i}\"\n",
        "\n",
        "    \"显示背景\"\n",
        "    scene bg win\n",
        "    \"使用scence显示背景，bg win可以是'bg win.jpg'或者'bg win.png'或者其他图像格式\"\n",
        "\n",
        "    # 显示角色立绘。此处使用了占位图，但您也可以在图片目录添加命名为\n",
        "    # “eileen happy.png”的文件来将其替换掉。\n",
        "    \"显示人物图像\"\n",
        "    show eileen happy\n",
        "    \"{i}show eileen happy{/i}\"\n",
        "    \"未提前定义的图片也可直接通过'show'引用\"\n",
        "    \"RenPy 会自动去 Images 文件夹搜索 'eileen happy'.jpg/.png等\"\n",
        "\n",
        "    # 此处显示各行对话。\n",
        "    define e = Character(\"艾琳\", image=\"eileen\", color='#e3b4b8')\n",
        "    image eileen concerned = \"character/eileen concerned.png\"           # 路径为: '.\\images\\character\\xxx.jpg' images省略，默认从images搜索图片\n",
        "    image side eileen concerned = \"character/side eileen concerned.png\" # side设置头像\n",
        "    image eileen happy = \"character/eileen happy.png\"\n",
        "    image side eileen happy = \"character/side eileen happy.png\"\n",
        "    image eileen vhappy = \"character/eileen vhappy.png\"\n",
        "    image side eileen vhappy = \"character/side eileen vhappy.png\"\n",
        "    \n",
        "    e \"定义角色\\{艾琳\\}，同时定义其图片\"\n",
        "    e concerned \"艾琳 {b}concerned{/b}\"\n",
        "    e happy \"艾琳 {b}happy{/b}\"\n",
        "    e vhappy \"艾琳 {b}very happy{/b}\"\n",
        "    e \"可以看到头像随人物图像的变动而变动\"\n",
        "\n",
        "    e \"语音\"\n",
        "    voice \"audio/D12_945.wav\"\n",
        "    e \"此外，鲨鱼，鱼龙和海豚仅管外貌上很相似，都擅长游泳，有人把它们归属鱼类\"\n",
        "    e \"具体参考 https://www.renpy.cn/doc/voice.html\"\n",
        "    \n",
        "    hide eileen\n",
        "    \"使用hide来隐藏图像，达到角色退场的效果\"\n",
        "    \"注意是hide图像eileen，而不是hide角色e\"\n",
        "    e concerned \"角色退场后说话\"\n",
        "    show eileen happy\n",
        "    e \"重新展示角色\"\n",
        "\n",
        "    scene bg blank with fade\n",
        "    # region 转场特效\n",
        "        # None 无转场特效\n",
        "        # fade          0.5秒时间画面逐渐暗淡至全黑，然后0.5秒时间画面从全黑逐渐变亮成新界面。\n",
        "        # dissolve      0.5秒时间，使用溶解效果从旧界面切到新界面。\n",
        "        # pixellate     0.5秒像素化旧场景，并0.5秒反向像素化至新场景。\n",
        "        # move          通过在图像上移动位置切换场景。\n",
        "        # moveinright   从界面上对应的边界移入图像，用时0.5秒。(moveinleft, moveintop, moveinbottom)\n",
        "        # moveoutright  从界面上对应的边界移出图像，用时0.5秒。(moveoutleft, moveouttop, moveoutbottom)\n",
        "        # ease          类似于上面的move系列转场效果，差别在于ease系列基于余弦曲线放缓开始和结束的转场。(easeinright, easeinleft, easeintop, easeinbottom, easeoutright, easeoutleft, easeouttop, easeoutbottom)\n",
        "        # zoomin        镜头放大切入图像，耗时0.5秒。\n",
        "        # zoomout       镜头缩小离开图像，耗时0.5秒。\n",
        "        # zoominout     先镜头放大切入图像，然后镜头缩小离开图像，耗时0.5秒。\n",
        "        # vpunch        这种转场效果，会垂直摇晃界面0.25秒。\n",
        "        # hpunch        这种转场效果，会水平摇晃界面0.25秒。\n",
        "        # blinds        垂直遮蔽原界面，耗时1秒。\n",
        "        # squares       以平面效果转场界面，耗时1秒。\n",
        "        # wipeleft      以指定方向擦除原界面。 (wiperight, wipeup, wipedown)\n",
        "        # slideleft     以指定方向滑入新场景。(slideright, slideup, slidedown)\n",
        "        # slideawayleft 以指定方向滑出旧场景。(slideawayright, slideawayup, slideawaydown)\n",
        "        # pushright     新场景把旧场景从指定的边界推出。(pushleft, pushup, pushdown)\n",
        "        # irisin        使用一个矩形iris显示新界面，或者隐藏旧界面。(irisout)\n",
        "    # endregion\n",
        "    '重新调用{i}scene{/i}则清除所有图像并显示一个背景图'\n",
        "    '{i}with fade{/i}表示特效为淡出'\n",
        "    # +-----------------------------------------------------------+\n",
        "    # |topleft, reset               top                   topright|\n",
        "    # |                                                           |\n",
        "    # |                                                           |\n",
        "    # |                                                           |\n",
        "    # |                                                           |\n",
        "    # |                          truecenter                       |\n",
        "    # |                                                           |\n",
        "    # |                                                           |\n",
        "    # |                                                           |\n",
        "    # |                                                           |\n",
        "    # |left                   center, default                right|\n",
        "    # +-----------------------------------------------------------+\n",
        "\n",
        "    '背景音乐'\n",
        "    play music \"audio/Piano Sonata no. 11, K. 331 - I. Andante grazioso.mp3\" fadeout 1.0 fadein 1.0\n",
        "    'fadeout and fadein分句用于旧音乐的淡出和新音乐的淡入'\n",
        "\n",
        "    show eileen at left\n",
        "    e concerned \"在左侧显示人像\"\n",
        "    show eileen at right with move\n",
        "    e concerned \"移动到右侧\"\n",
        "\n",
        "    define l = Character(\"露西\", image=\"lucy\", color='#92b3a5')\n",
        "    image lucy happy = \"character/lucy happy.png\"\n",
        "    image lucy mad = \"character/lucy mad.png\"\n",
        "\n",
        "    show lucy happy at left\n",
        "    l happy \"增加一个角色\"\n",
        "\n",
        "    l mad \"对话合并    可以自定义样式\"(multiple=2)\n",
        "    e happy \"对话合并\"(multiple=2)\n",
        "\n",
        "    \"python语句\"\n",
        "    \"单行用{i}${\\i}表示\"\n",
        "    \"如{i}$ count = 0{\\i}\"\n",
        "    $ count = 0\n",
        "    '使用\\[{i}var{\\i}\\]调用变量'\n",
        "    \"这里count = [count]\"\n",
        "\n",
        "    \"Ren'Py中if-else可以直接使用\"\n",
        "\n",
        "    if 0:\n",
        "        $ count = 0\n",
        "    elif -1:\n",
        "        $ count = -1\n",
        "    else:\n",
        "        $ count = 1\n",
        "    \n",
        "    \"count现在是: [count]\"\n",
        "\n",
        "    \"多行python语句\"\n",
        "    python:\n",
        "        for i in range(1,5,2):\n",
        "            count += i\n",
        "            print(f'count现在是: {count}')\n",
        "\n",
        "    \"count现在是: [count]\"\n",
        "\n",
        "    \"使用多行python来读取输入\"\n",
        "    define pov = Character(\"[povname]\")\n",
        "    python:\n",
        "        # python 代码块\n",
        "        povname = renpy.input(\"你的名字是什么？\", length=32)\n",
        "        povname = povname.strip()\n",
        "\n",
        "        if not povname:\n",
        "            povname = \"122333\"\n",
        "\n",
        "    pov \"我的名字是 [povname]!\"\n",
        "\n",
        "    \"使用manu来进行分支选择\"\n",
        "    \"关于 import 引用，以下选项中描述错误的是( )\"\n",
        "    $ ans = 0\n",
        "    menu:\n",
        "        'A. 使用 import numpy 引入 numpy 库':\n",
        "            $ ans = 1\n",
        "        'B. 可以使用 from numpy import Pi 引入 numpy 库':\n",
        "            $ ans = 2\n",
        "        'C. 使用 import numpy as np 引入 numpy 库，取别名为 np':\n",
        "            $ ans = 3\n",
        "        'D. import 保留字用于导入模块或者模块中的对象':\n",
        "            $ ans = 4\n",
        "    \n",
        "    if ans == 2:\n",
        "        \"回答正确\"\n",
        "    else:\n",
        "        \"回答错误\"\n",
        "\n",
        "    nvl_narrator 'NVL模式'\n",
        "    nvl_narrator 'NVL模式指的是一张背景图，全屏文本的模式'\n",
        "    nvl_narrator '适合背景介绍，旁白等'\n",
        "    \n",
        "    nvl_narrator \"定义NVL内角色玛丽\"\n",
        "    define m = Character(\"玛丽\", kind=nvl, color='#c8c8ff')\n",
        "    m \"我是玛丽\"\n",
        "\n",
        "    e 'NVL外的角色说话即离开NVL'\n",
        "    m 'NVL内的角色说话回到NVL'\n",
        "    \n",
        "    nvl_narrator \"可以使用'nvl clear'来清空屏幕\"\n",
        "    nvl clear\n",
        "    nvl_narrator \"现在NVL的内容清空了\"\n",
        "\n",
        "    \"下面介绍标签\"\n",
        "    \"标签类似面向过程编程的标签，或者内存中的跳转\"\n",
        "    \"面向过程编程中，按照自上而下的顺序逐行执行，也就是说，只有第一行命令执行之后，才可能执行第二行，第二行执行之后，才可以执行到第三行.....如此依次执行\"\n",
        "    \"使用流程跳转\"\n",
        "    \"jump语句用于将主控流程转入给定的脚本标签(label)处而call语句允许主控流程在执行完这次调用后，回到调用发生的脚本位置。\"\n",
        "\n",
        "    menu:\n",
        "        \"尝试jump跳转标签\":\n",
        "            jump .a1\n",
        "        \n",
        "        \"尝试call跳转标签\":\n",
        "            call .a1\n",
        "    # 此处为游戏结尾。\n",
        "    \"结尾\"\n",
        "    return \n",
        "\n",
        "label .a1:\n",
        "    \"这里是a1标签\"\n",
        "\n",
        "label .a2:\n",
        "    \"这里是a2标签\"\n",
        "    return\n",
        "\n",
        "label quit:\n",
        "    e \"退出标签quit\"\n",
        "    e \"若该标签存在，当用户从主菜单退出游戏时该标签内容会被调用。\"\n",
        "    \"{b}{size=45}{alpha=0.7}{color=#ed556a}END{/color}{/alpha}{/size}{/b}\"\n",
        "    return\n"
      ],
      "id": "9966d88d"
    }
  ],
  "metadata": {
    "kernelspec": {
      "name": "python3",
      "language": "python",
      "display_name": "Python 3 (ipykernel)"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 5
}